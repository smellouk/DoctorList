apply plugin: 'doctor-list-app'
apply from: 'git-version.gradle'

android {
    defaultConfig {
        applicationId Config.appId
        versionCode gitVersionCode
        versionName gitVersionName
    }

    signingConfigs {
        if (localProperties.containsKey("storePassword") && localProperties.containsKey("keyAlias")
                && localProperties.containsKey("keyPassword")) {
            release {
                storeFile file("../.signing/release.jks")
                storePassword localProperties['storePassword']
                keyAlias localProperties['keyAlias']
                keyPassword localProperties['keyPassword']
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = '.debug'
            resValue "string", "app_name", "Doctor List - Debug"
        }

        release {
            resValue "string", "app_name", "Doctor List"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            //Add signing if config exists
            if (localPropertiesFile.canRead()) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    implementation project(":common")
    implementation project(":repositories")
    implementation project(":features")

    implementation Libs.appcompat
    implementation Libs.kotlin_stdlib

    implementation Libs.dagger
    kapt Libs.dagger_compiler

    //chucker library
    implementation Libs.okhttp
    debugImplementation Libs.library
    releaseImplementation Libs.library_no_op
}