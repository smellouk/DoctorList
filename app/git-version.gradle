buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath Libs.grgit
    }
}

import org.ajoberstar.grgit.Grgit

ext {
    try {
        grgit = Grgit.open(currentDir: projectDir)

        def gitNameBranch = grgit.branch.current().name
        gitVersionName = grgit.describe()
        gitVersionCode = grgit.log(includes: [gitNameBranch]).size()
        gitCommitHash = grgit.head().abbreviatedId

        if (gitVersionName == null) {
            gitVersionName = "$gitNameBranch-$gitCommitHash"
        } else if (gitNameBranch == "master") {
            gitVersionName = "$gitVersionName ($gitVersionCode)"
        } else {
            gitVersionName = "$gitVersionName ($gitNameBranch:$gitCommitHash)"
        }
    } catch (Exception ignore) {
        //Default when there isn't .git
        gitVersionName = "1.0"
        gitVersionCode = 1
        gitCommitHash = ""
    }

}

task printVersion() {
    println("Version Name: $gitVersionName")
    println("Version Code: $gitVersionCode")
    println("Commit Hash: $gitCommitHash")
}