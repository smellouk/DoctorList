apply plugin: "de.fayard.buildSrcVersions"

buildscript {
    ext {
        localPropertiesFile = project.rootProject.file('local.properties')
        localProperties = new Properties()
        if (localPropertiesFile.canRead()) {
            localProperties.load(localPropertiesFile.newDataInputStream())
        }
    }

    repositories {
        google()
        jcenter()
        gradlePluginPortal()
    }

    dependencies {
        classpath Libs.com_android_tools_build_gradle
        classpath Libs.kotlin_gradle_plugin
        classpath Libs.de_fayard_buildsrcversions_gradle_plugin
        classpath Libs.grgit
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    afterEvaluate { project ->
        if (project.plugins.findPlugin('android') || project.plugins.findPlugin('android-library')) {
            android {
                compileSdkVersion Config.compileSdkVersion
                buildToolsVersion Config.buildToolsVersion

                compileOptions {
                    sourceCompatibility Config.javaVersion
                    targetCompatibility Config.javaVersion
                }

                kotlinOptions {
                    jvmTarget = Config.javaVersion
                }

                defaultConfig {
                    minSdkVersion Config.mindSdkVersion
                    targetSdkVersion Config.targetSdkVersion
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                    vectorDrawables.useSupportLibrary = true
                }

                buildTypes {
                    debug {
                        debuggable true
                        minifyEnabled false
                    }
                    release {
                        debuggable false
                        minifyEnabled true
                    }
                }

                flavorDimensions("endpoint")
                productFlavors {
                    dev {
                        dimension "endpoint"
                        if (localProperties.containsKey("devEndpoint")) {
                            buildConfigField 'String', 'HOST_URL', '"' + localProperties["devEndpoint"] + '"'
                        } else {
                            buildConfigField 'String', 'HOST_URL', '""'
                        }
                    }

                    prod {
                        dimension "endpoint"
                        if (localProperties.containsKey("prodEndpoint")) {
                            buildConfigField 'String', 'HOST_URL', '"' + localProperties["prodEndpoint"] + '"'
                        } else {
                            buildConfigField 'String', 'HOST_URL', '""'
                        }
                    }
                }

                variantFilter { variant ->
                    if (variant.buildType.name == 'release'
                            && variant.getFlavors().get(0).name == 'dev') {
                        variant.setIgnore(true)
                    }
                }
            }
        }

        dependencies {
            implementation Libs.kotlin_stdlib
        }
    }
}